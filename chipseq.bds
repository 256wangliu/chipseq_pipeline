#!/usr/bin/env bds
#vim: syntax=java


help == chipseq pipeline settings

pe 		:= false 	help Paired end data.
final_stage	:= "" 		help Final stage for pipeline (bam, filt_bam, tag, xcor and peak).
callpeak 	:= "spp,macs2"	help Peak calling method : spp and macs2, can choose both (default: 'spp,macs2').
true_rep   	:= false	help Call peaks on true replicates only.
ctl_depth_ratio := 1.2 		help Cut-off ratio of two control tagaligns for pooling (default: 1.2).
sigtrk 		:= ""		help (BETA) Signal track generation method : bam2bw (bamCoverage in deepTools) or tag2bw (align2rawsignal).
make_wig 	:= false	help Create wig (for '-sigtrk tag2bw' only).


help() // print help if no parameters are given

include "modules/input_fastq.bds"
include "modules/input_bam.bds"
include "modules/input_tagalign.bds"
include "modules/input_peak.bds"

include "modules/species.bds"

include "modules/report.bds"
include "modules/report_graphviz.bds"

include "modules/align_bwa.bds"
include "modules/postalign_bam.bds"
include "modules/postalign_bed.bds"

include "modules/callpeak_spp.bds"
include "modules/callpeak_macs2.bds"
include "modules/callpeak_gem.bds"
include "modules/idr.bds"

include "modules/signal.bds"


//////// Global variables /////

// system vars
input 		:= ""
num_ctl 	:= 1
num_callpeak 	:= 3 	// spp (1), macs2 (2), gem (3)
peakcallers 	:= ["spp","macs2","gem"]

// filepath of outputs
string{} fastq, bam, filt_bam, tag, tag_pr1, tag_pr2 // replicate data: map with key ("$ctl,$rep" or "$ctl,$rep,$pe" for PE fastqs)
string{} peak, peak_pr1, peak_pr2, peak_pooled, peak_ppr1, peak_ppr2 // peaks: map with key ("$rep")
string tag_pooled, tag_ctl_pooled, tag_ppr1, tag_ppr2
string idr_tr, idr_pr_rep1, idr_pr_rep2, idr_ppr, idr_opt, idr_consv, idr_tr_png, idr_pr_rep1_png, idr_pr_rep2_png, idr_ppr_png
string{} signal_trk, signal_trk_pval, signal_trk_fc	 // signal tracks from deepTools or bamCoverage: map with key ("$ctl,$rep")
string{} flagstat_qc, dup_qc, flagstat_nodup_qc, pbc_qc, xcor_qc, xcor_plot // QC logs: map with key ("$ctl,$rep" or "$ctl,$rep,$pe" for PE fastqs)
string{} xcor_qc_pr1, xcor_qc_pr2
string xcor_qc_ppr1, xcor_qc_ppr2
string idr_qc




//////// chipseq pipeline starts here /////

init_chipseq() // read command line parameters or configruation file

chk_input() // check input files are valid

align() // align and postalign

call_peaks() // call peaks

do_idr() // IDR

report()




void init_chipseq() {

	pe		= get_conf_val_bool( pe, 		["pe"] )
	final_stage	= get_conf_val( final_stage, 		["final_stage"] )

	callpeak	= get_conf_val( callpeak, 		["callpeak"] )
	true_rep 	= get_conf_val_bool( true_rep, 		["true_rep"] )
	ctl_depth_ratio = get_conf_val_real( ctl_depth_ratio, 	["ctl_depth_ratio"])
		
	sigtrk 		= get_conf_val( sigtrk, 		["sigtrk"] )

	// determine input type
	if ( get_peak(1,0) != "" ) 	input = "peak"
	if ( get_tag(0,1) != "" ) 	input = "tag"
	if ( get_filt_bam(0,1) != "" ) 	input = "filt_bam"
	if ( get_bam(0,1) != "" ) 	input = "bam"
	if ( get_fastq(0,1,1) != "" ) 	input = "fastq"

	if ( !control_exists() ) num_ctl = 0

	print_chipseq()
	
	out_dir = mkdir_path( out_dir ) // create output directory and get absolute path for it	
}

void print_chipseq() {

	print( "\n\n== chipseq pipeline settings\n")
	print( "Final stage for ChIP-Seq\t: $final_stage\n" )
	print( "# replicates \t\t\t: "+get_num_rep()+"\n" )
	print( "Input data type\t\t\t: $input\n")
	print( "Peak calling method\t\t: $callpeak\n" )
	print( "Peak calling for true reps only\t: $true_rep\n" )
	print( "Control rep. depth ratio\t: $ctl_depth_ratio\n" )
	print( "Sig. trk. generation method\t: $sigtrk\n" )
	print( "Create wig\t\t\t: $make_wig\n" )
}

void chk_input() {

	print( "\n\n== checking chipseq inputs (data type = $input) ...\n\n" );

	if ( is_input_fastq() ) 	chk_align_bwa()
	if ( is_callpeak_gem() ) 	chk_callpeak_gem()
	if ( is_callpeak_macs2() ) 	chk_callpeak_macs2()
	if ( is_sigtrk_aln2rawsig() ) 	chk_signal_aln2rawsig()
	if ( is_final_stage_idr() ) 	chk_idr()

	if ( is_align_only_mode() ) { 	
		print("Align-only mode (no peak calling) for " + get_num_rep() + " replicates.\n")
	}
	else if ( get_num_rep() > 2 ) {
		error("For calling peaks, # replicates (" + get_num_rep() + ") should be <= 2.\n")
	}

	print("\n")

	if ( is_input_peak() ) {

		for ( int rep=0; rep<3; rep++) {

			for (int pse=0; pse<3; pse++) {

				peak := get_peak(rep,pse)
				prefix := (rep==0 ? "pooled " : "") + (pse==0 ? "replicate $rep" : "pseudo-replicate $pse for replicate $rep")
				
				print( "$prefix: \n\t$peak\n")
			}
		}

		if ( get_peak(0,0)=="" || get_peak(1,0)=="" || get_peak(2,0)=="" ) { // if (!can_idr_for_true_rep() ) {
			error("Cannot do IDR for true replicates. Check your peak inputs.\n")
		}

		return
	}

	string[] data_all

	for ( int ctl=0; ctl <= num_ctl; ctl++) { // iterate through replicats (0: not control, 1~: controls)

		for ( int rep=1; rep <= get_num_rep(); rep++) {

			string[] data

			prefix := (ctl==1) ? "Control " : ""
			suffix := is_paired_end( ctl, rep ) ? " (PE)" : " (SE)"

			if ( is_input_fastq() ) {
				prefix = prefix + "Rep$rep fastq" + suffix
				fastqs := get_fastqs( ctl, rep )
				if ( fastqs.size()==0 ) {
					data.push( "" )
				}
				else {
					for ( string fastq : fastqs ) data.push( fastq )
				}
			}
			else if ( is_input_bam() ) {
				prefix = prefix +"Rep$rep bam" + suffix
				data.push( get_bam( ctl, rep ) )
			}
			else if ( is_input_filt_bam() ) {
				prefix = prefix +"Rep$rep filt_bam" + suffix
				data.push( get_filt_bam( ctl, rep ) )
			}
			else if ( is_input_tag() ) {
				prefix = prefix + "Rep$rep tagalign" + suffix
				data.push( get_tag( ctl, rep ) )
			}

			print("$prefix :\n")

			for ( string s : data ) {
				print("\t$s\n")
				if ( (s != "") && !path_exists(s) ) error("\t\tFile not found!\n")
			}

			// if data is missing
			if ( data[0] == "" ) {
				if ( (rep==2) && (ctl==1) ) \
					print( "\tWarning: $prefix missing! keep going... (using control 1 for calling peaks on replicate 2)\n")
				else if ( (rep==2) && (ctl==0) ) \
					print( "\tWarning: $prefix missing! keep going... (peak calling for replicate 1 only)\n")
				else \
					error( "\t$prefix missing!\n")
				continue
			}

			// check any duplicate input filename
			for ( string s : data ) {
				if ( is_in_array( get_basename( s ), get_basename( data_all ) ) ) \
					error( "\t$prefix has duplicate filename!\n")
			}

			data_all = concat( data_all, data )
		}
	}

	if ( final_stage != "" ) \
		print( "\n====== Final stage : $final_stage \n\n" );
}

void align() {

	// parallel jobs for align() for each replicate and each control
	for ( int ctl=0; ctl <= num_ctl; ctl++) { // iterate through inputs (ctl==0 : replicate, ctl==1 : control)	
	
		for ( int rep=1; rep <= get_num_rep(); rep++) {

			if ( !is_data_available( ctl, rep ) ) continue

			par _align( ctl, rep )
		}
	}

	wait
}

void _align( int ctl, int rep ) {

	if ( is_single_ended( ctl, rep ) ) 	_align_SE( ctl, rep )
	else 					_align_PE( ctl, rep )

	wait_par_rep() // count # of replicates being processed and wait (barrier) par.
}

void _align_SE( int ctl, int rep ) {

	info 	:= get_info( ctl, rep )
	key 	:= "$ctl,$rep"  // key name for global output variable (map)

	align_out_dir := mkdir_path( "$out_dir/align_$info" ) // create align output directory

	if ( is_input_fastq() ) {

		fastqs := get_fastqs( ctl, rep )

		fastq{key} = fastqs[0]

		( bam{key}, flagstat_qc{key} ) = _bwa( fastq{key}, align_out_dir, info )
		wait

		if ( is_final_stage_bam() ) return
	}

	if ( is_input_bam() || is_input_fastq() ) {
	
		if ( is_input_bam() ) bam{key} = get_bam( ctl, rep )

		( filt_bam{key}, dup_qc{key}, flagstat_nodup_qc{key}, pbc_qc{key} ) = _dedup_bam( bam{key}, align_out_dir, info )
		wait

		if ( is_sigtrk_deeptools() ) {

			signal_trk{key} = _bam_to_bw( filt_bam{key}, align_out_dir, info )
		}

		if ( is_final_stage_filt_bam() ) return
	}

	if ( is_input_filt_bam() || is_input_bam() || is_input_fastq() ) {

		if ( is_input_filt_bam() ) filt_bam{key} = get_filt_bam( ctl, rep )

		tag{key} = _bam_to_tag( filt_bam{key}, align_out_dir, info, \
					"filt_bam\\n($info)", "tagalign\\n($info)" )
		wait

		if ( is_final_stage_tag() ) return
	}

	if ( is_input_tag() || is_input_filt_bam() || is_input_bam() || is_input_fastq() ) {

		if ( is_input_tag() ) tag{key} = get_tag( ctl, rep )

		subsampled_tag := _subsample_tag( tag{key}, align_out_dir, info )

		if ( ctl == 0 ) {

			( xcor_qc{key}, xcor_plot{key} ) = _xcor( subsampled_tag, align_out_dir, info )
			wait

			if ( is_sigtrk_aln2rawsig() ) {

				fraglen := get_fraglen( xcor_qc{key} )
				
				signal_trk{key} = _tag_to_bw( tag{key}, fraglen, align_out_dir, info )

				if ( make_wig ) _tag_to_wig( tag{key}, fraglen, align_out_dir, info )

				//wait
			}
		}

		if ( is_final_stage_xcor() ) return
	}

	if ( ctl == 0 ) { // if replicate

		(tag_pr1{key}, tag_pr2{key} ) = _spr( tag{key}, align_out_dir, info ) // make self pseudo replicate
		wait

		// xcor for pseudo replicates
		string tmp	
		( xcor_qc_pr1{key}, tmp ) = _xcor( tag_pr1{key}, align_out_dir, info+"_pr1", "", "" ) // suppress graphviz
		( xcor_qc_pr2{key}, tmp ) = _xcor( tag_pr2{key}, align_out_dir, info+"_pr2", "", "" )
		wait
	}
}

void _align_PE( int ctl, int rep ) {

	info 	:= get_info( ctl, rep )
	key 	:= "$ctl,$rep"  // key name for global output variable (map)

	align_out_dir := mkdir_path( "$out_dir/align_$info" ) // create align output directory

	if ( is_input_fastq() ) {

		fastqs := get_fastqs( ctl, rep )

		fastq1 := fastqs[0]
		fastq2 := fastqs[1]

		fastq{key+",1"} = fastq1
		fastq{key+",2"} = fastq2

		( bam{key}, flagstat_qc{key} ) = _bwa_PE( fastq1, fastq2, align_out_dir, info )
		wait

		if ( is_final_stage_bam() ) return
	}

	if ( is_input_bam() || is_input_fastq() ) {

		if ( is_input_bam() ) bam{key} = get_bam( ctl, rep )

		( filt_bam{key}, dup_qc{key}, flagstat_nodup_qc{key}, pbc_qc{key} ) = _dedup_bam_PE( bam{key}, align_out_dir, info )
		wait

		if ( is_sigtrk_deeptools() ) { // signal track generation (deeptools)

			signal_trk{key} = _bam_to_bw( filt_bam{key}, align_out_dir, info )
		}

		if ( is_final_stage_filt_bam() ) return
	}
	
	if ( is_input_filt_bam() || is_input_bam() || is_input_fastq() ) {

		if ( is_input_filt_bam() ) filt_bam{key} = get_filt_bam( ctl, rep )

		tag{key} = _bam_to_tag( filt_bam{key}, align_out_dir, info, \
					"filt_bam\\n($info)", "tagalign\\n($info)" )
		wait

		if ( is_final_stage_tag() ) return
	}

	string bedpe, subsampled_tag

	if ( is_input_tag() || is_input_filt_bam() || is_input_bam() || is_input_fastq() ) {

		if ( is_input_tag() ) {

			tag{key} = get_tag( ctl, rep )

			subsampled_tag = _subsample_tag_PE( tag{key}, align_out_dir, info )
			wait
		}
		else {
			/*
			nmsrt_bam := _nmsrt_bam( filt_bam{key}, align_out_dir, info )
			wait

			bedpe = _bam_to_bedpe( nmsrt_bam, align_out_dir, info )
			wait
			*/

			bedpe = _bam_to_bedpe( filt_bam{key}, align_out_dir, info, \
						"filt_bam\\n($info)", "bedpe\\n($info)" )
			wait

			subsampled_tag = _subsample_bedpe_to_tag( bedpe, align_out_dir, info )
			wait
		}

		if ( ctl == 0 ) {

			( xcor_qc{key}, xcor_plot{key} ) = _xcor( subsampled_tag, align_out_dir, info, \
								"subsampled\\ntagalign\\n($info)", "xcor_qc\\n($info)")
			wait

			if ( is_sigtrk_aln2rawsig() ) {

				fraglen := get_fraglen( xcor_qc{key} )

				signal_trk{key} = _tag_to_bw( tag{key}, fraglen, align_out_dir, info )
				
				if ( make_wig ) _tag_to_wig( tag{key}, fraglen, align_out_dir, info )
			}
		}

		if ( is_final_stage_xcor() ) return
	}

	if ( ctl == 0 ) { // if replicate

		if ( is_input_tag() ) { // if starting from tag, we don't have bedpe file			
			( tag_pr1{key}, tag_pr2{key} ) = _spr_tag_PE( tag{key}, align_out_dir, info )
		}
		else {
			( tag_pr1{key}, tag_pr2{key} ) = _spr_PE( bedpe, align_out_dir, info )
		}
		wait

		// xcor for pseudo replicates
		string tmp
		( xcor_qc_pr1{key}, tmp ) = _xcor( tag_pr1{key}, align_out_dir, info+"_pr1", "", "" ) // suppress graphviz
		( xcor_qc_pr2{key}, tmp ) = _xcor( tag_pr2{key}, align_out_dir, info+"_pr2", "", "" )
		wait
	}
}

void call_peaks() {

	if ( is_align_only_mode() || is_input_peak() ) return

	if ( !(is_final_stage_peak() || is_final_stage_idr()) ) return

	_pool_tags()

	for ( int i=0; i<num_callpeak; i++ ) {

		par _call_peaks( peakcallers[i] ) // go parallel
	}

	wait
}

void _pool_tags() {

	// pool replicates and controls, then create ppr (pooled pseudoreplicates) before peak calling

 	align_pooled_dir := mkdir_path( "$out_dir/align_pooled" )

	if ( is_data_available( 0, 2 ) ) { // if replicate 2 exists, pool replicate 1 and replicate 2

		(tag_pooled, tag_ppr1, tag_ppr2 ) = _ppr( tag{"0,1"}, tag_pr1{"0,1"}, tag_pr2{"0,1"}, \
							  tag{"0,2"}, tag_pr1{"0,2"}, tag_pr2{"0,2"}, align_pooled_dir, "" ) // make pooled psudo replicate
		wait

		// cross correlation analysis to get fragment of ppr's
		string tmp

		(xcor_qc_ppr1, tmp) = _xcor( tag_ppr1, align_pooled_dir, "ppr1", "", "" )
		(xcor_qc_ppr2, tmp) = _xcor( tag_ppr2, align_pooled_dir, "ppr2", "", "" )
		wait
	}

	if ( is_data_available( 1, 2 ) ) { // if control 2 exists, pool control 1 and control 2

		tag_ctl_pooled 	= _pool_tag( tag{"1,1"}, tag{"1,2"}, align_pooled_dir, "ctl" )
		wait

		_add_to_graphviz( ["tagalign\\n(ctl1)","tagalign\\n(ctl2)"], [tag{"1,1"},tag{"1,2"}], \
				  ["tagalign\\n(ctl, pooled)"], [tag_ctl_pooled] )
	}
}

void _call_peaks( string peakcaller ) {

	if ( peakcaller == "spp" && !(is_callpeak_spp()) ) return
	if ( peakcaller == "macs2" && !(is_callpeak_macs2()) ) return
	if ( peakcaller == "gem" && !(is_callpeak_gem()) ) return

	// get fragment length from cross corr. qc log
	string fraglen_ppr1, fraglen_ppr2, fraglen_mean
	string{} fraglen, fraglen_pr1, fraglen_pr2 // key = replicate id

	fraglen{1} = get_fraglen( xcor_qc{"0,1"} ) // get fragment length of replicate 1
	fraglen_pr1{1} = get_fraglen( xcor_qc_pr1{"0,1"} )
	fraglen_pr2{1} = get_fraglen( xcor_qc_pr2{"0,1"} )

	if ( is_data_available( 0, 2 ) ) { // if replicate 2 exists, get mean fraglen and fraglen for ppr

		fraglen{2} = get_fraglen( xcor_qc{"0,2"} ) // get fragment length of replicate 2
		fraglen_pr1{2} = get_fraglen( xcor_qc_pr1{"0,2"} )
		fraglen_pr2{2} = get_fraglen( xcor_qc_pr2{"0,2"} )
		fraglen_mean 	= round( (fraglen{1}.parseReal()+fraglen{2}.parseReal())*0.5 ) // compute fraglen mean for pooled

		fraglen_ppr1 = get_fraglen( xcor_qc_ppr1 )
		fraglen_ppr2 = get_fraglen( xcor_qc_ppr2 )
	}

	// choose appropriate control

	string tag_ctl1_graphviz_id, tag_ctl2_graphviz_id, tag_ctl_pooled_graphviz_id
	string tag_ctl1_, tag_ctl2_, tag_ctl_pooled_

	if ( !control_exists() ) {

		if ( peakcaller != "macs2" ) return

		tag_ctl1_ = ""
		tag_ctl2_ = ""
		tag_ctl_pooled_ = ""

		tag_ctl1_graphviz_id = ""
		tag_ctl2_graphviz_id = ""

		tag_ctl_pooled_graphviz_id = ""
	}
	else if ( is_data_available( 1, 2 ) ) { // if control 2 exists

		tag_ctl1_ = tag{"1,1"}
		tag_ctl2_ = tag{"1,2"}
		tag_ctl_pooled_ = tag_ctl_pooled

		tag_ctl_pooled_graphviz_id = "tagalign\\n(ctl, pooled)"

		real ntags_rep1 = get_no_lines( tag{"0,1"} )
		real ntags_rep2 = get_no_lines( tag{"0,2"} )		
		real ntags_ctl1 = get_no_lines( tag{"1,1"} )
		real ntags_ctl2 = get_no_lines( tag{"1,2"} )
		wait

		ratio_ctl_reads := ntags_ctl1/ntags_ctl2

		if ( ratio_ctl_reads < 1.0 ) ratio_ctl_reads = 1.0/ratio_ctl_reads		
		
		if ( ratio_ctl_reads > ctl_depth_ratio ) {

			print("\n\nNumber of reads in controls differ by a factor of $ctl_depth_ratio. Using pooled controls.\n" )

			tag_ctl1_ = tag_ctl_pooled
			tag_ctl2_ = tag_ctl_pooled

			tag_ctl1_graphviz_id = "tagalign\\n(ctl, pooled)"
			tag_ctl2_graphviz_id = "tagalign\\n(ctl, pooled)"
		}
		else {
			if ( ntags_ctl1 < ntags_rep1 ) {

				print("\n\nFewer reads in control replicate 1 than experiment replicate 1. Using pooled controls for replicate 1.\n")

				tag_ctl1_ = tag_ctl_pooled

				tag_ctl1_graphviz_id = "tagalign\\n(ctl, pooled)"
				tag_ctl2_graphviz_id = "tagalign\\n(ctl2)"
			}
			if ( ntags_ctl2 < ntags_rep2 ) {

				print("\n\nFewer reads in control replicate 2 than experiment replicate 2. Using pooled controls for replicate 2.\n")

				tag_ctl2_ = tag_ctl_pooled

				tag_ctl1_graphviz_id = "tagalign\\n(ctl1)"
				tag_ctl2_graphviz_id = "tagalign\\n(ctl, pooled)"
			}
		}

	}
	else { // otherwise, no pooling. use control 1 for all replicates

		tag_ctl1_ 	= tag{"1,1"}
		tag_ctl2_ 	= tag{"1,1"}
		tag_ctl_pooled_	= tag{"1,1"}

		tag_ctl1_graphviz_id 		= "tagalign\\n(ctl1)"
		tag_ctl2_graphviz_id 		= "tagalign\\n(ctl1)"

		tag_ctl_pooled_graphviz_id 	= "tagalign\\n(ctl1)"
	}

	// create directories
	peak_out_dir 	:= mkdir_path( "$out_dir/peaks_$peakcaller") // peak directory structure
	truerep_dir	:= mkdir_path( "$peak_out_dir/true_replicates" )

	string pseudorep_dir, ppr_dir

	if ( !true_rep ) {
		pseudorep_dir 	= mkdir_path( "$peak_out_dir/pseudo_replicates" )
		ppr_dir 	= mkdir_path( "$peak_out_dir/pooled_pseudo_replicates" )
	}

	//////////////// call peaks /////////////////

	peak{"$peakcaller,1"} = \
		_call_peaks( peakcaller, tag{"0,1"}, tag_ctl1_, fraglen{1}, truerep_dir, "rep1", \
				"tagalign\\n(rep1)", tag_ctl1_graphviz_id )

	if ( !true_rep ) {
		
		peak_pr1{"$peakcaller,1"} = \
			_call_peaks( peakcaller, tag_pr1{"0,1"}, tag_ctl1_, fraglen_pr1{1}, pseudorep_dir, "rep1_pr1", \
				"tagalign pr1\\n(rep1)",tag_ctl1_graphviz_id )

		peak_pr2{"$peakcaller,1"} = \
			_call_peaks( peakcaller, tag_pr2{"0,1"}, tag_ctl1_, fraglen_pr2{1}, pseudorep_dir, "rep1_pr2", \
				"tagalign pr2\\n(rep1)", tag_ctl1_graphviz_id )
	}

	if ( is_data_available( 0, 2 ) ) { // if replicate 2 exists

		peak{"$peakcaller,2"} 	= \
			_call_peaks( peakcaller, tag{"0,2"}, tag_ctl2_, fraglen{2}, truerep_dir, "rep2", \
				"tagalign\\n(rep2)", tag_ctl2_graphviz_id )

		peak_pooled{"$peakcaller"}= \
			_call_peaks( peakcaller, tag_pooled, tag_ctl_pooled_, fraglen_mean, truerep_dir, "pooled", \
				"tagalign\\n(pooled)", tag_ctl_pooled_graphviz_id )

		if ( !true_rep ) {

			peak_pr1{"$peakcaller,2"} = \
				_call_peaks( peakcaller, tag_pr1{"0,2"}, tag_ctl2_, fraglen_pr1{2}, pseudorep_dir, "rep2_pr1", \
					"tagalign pr1\\n(rep2)",tag_ctl2_graphviz_id )

			peak_pr2{"$peakcaller,2"} = \
				_call_peaks( peakcaller, tag_pr2{"0,2"}, tag_ctl2_, fraglen_pr2{2}, pseudorep_dir, "rep2_pr2", \
					"tagalign pr2\\n(rep2)",tag_ctl2_graphviz_id )

			peak_ppr1{"$peakcaller"} = \
				_call_peaks( peakcaller, tag_ppr1, tag_ctl_pooled_, fraglen_ppr1, ppr_dir, "ppr1", \
					"tagalign\\n(ppr1)",tag_ctl_pooled_graphviz_id )

			peak_ppr2{"$peakcaller"} = \
				_call_peaks( peakcaller, tag_ppr2, tag_ctl_pooled_, fraglen_ppr2, ppr_dir, "ppr2", \
					"tagalign\\n(ppr2)",tag_ctl_pooled_graphviz_id )
		}
	}
}

string _call_peaks( string peakcaller, string tag1, string tag_ctl, string fraglen, string o_dir, string label ) {

	return _call_peaks( peakcaller, tag1, tag_ctl, fraglen, o_dir, label, "tagalign\\n(rep1)", "tagalign\\n(ctl1)" )
}

string _call_peaks( string peakcaller, string tag1, string tag_ctl, string fraglen, string o_dir, string label, \
			string label_in1, string label_in2 ) {
	
	if ( peakcaller == "spp" ) {
		
		string narrowpeak, regionpeak, score, pdf 
		( narrowpeak, regionpeak, score, pdf ) = _spp( tag1, tag_ctl, fraglen, o_dir, label, label_in1, label_in2 )

		return regionpeak // use regionpeak instead of narrowpeak
	}
	else if ( peakcaller == "macs2" ) {
		
		string narrowpeak, bpeak, gpeak, fc_bw, pval_bw
		( narrowpeak, bpeak, gpeak, fc_bw, pval_bw ) = _macs2( tag1, tag_ctl, fraglen, o_dir, label, label_in1, label_in2 )

		// macs2 generates signal tracks
		signal_trk_pval{label} 		= pval_bw
		signal_trk_fc{label} 		= fc_bw

		return narrowpeak // use narrowpeak
	}
	/*	
	else if ( peakcaller == "gem" ) {
		
		string peak, ksm, pfm, html
		( peak, ksm, pfm, html ) = _gem( tag1, tag_ctl, o_dir, label )

		return peak
	}
	*/
}

void do_idr() {

	if ( !is_final_stage_idr() || !is_callpeak_spp() || !is_data_available( 0, 2 ) ) return

	string idr_out_dir

	if ( is_input_peak() ) {

		//idr_out_dir = mkdir_path( "$out_dir/" + (use_idr1 ? "idr1" : "idr2") )
		idr_out_dir = mkdir_path( "$out_dir/idr2" )

		peak{"spp,1"} = get_peak(1,0);	peak{"spp,2"} = get_peak(2,0);	peak_pooled{"spp"} = get_peak(0,0)
		peak_pr1{"spp,1"} = get_peak(1,1);	peak_pr2{"spp,1"} = get_peak(1,2)
		peak_pr1{"spp,2"} = get_peak(2,1); 	peak_pr2{"spp,2"} = get_peak(2,2)
		peak_ppr1{"spp"} = get_peak(0,1); 	peak_ppr2{"spp"} = get_peak(0,2)
	}
	else {
		//idr_out_dir = mkdir_path( "$out_dir/peaks_spp/" + (use_idr1 ? "idr1" : "idr2") )
		idr_out_dir = mkdir_path( "$out_dir/peaks_spp/idr2" )
	}

	// take 2nd and 3rd output (narrowpeak.gz, and png plot) of _idr
	(idr_tr, idr_tr_png) = _idr2( peak{"spp,1"}, peak{"spp,2"}, peak_pooled{"spp"}, idr_out_dir, "true", \
					"peak_spp\\n(rep1)","peak_spp\\n(rep2)","peak_spp\\n(pooled)" )

	if ( !true_rep ) {

		(idr_pr_rep1, idr_pr_rep1_png) = _idr2( peak_pr1{"spp,1"}, peak_pr2{"spp,1"}, peak{"spp,1"}, idr_out_dir, "rep1_pr", \
					"peak_spp\\n(rep1_pr1)","peak_spp\\n(rep1_pr2)","peak_spp\\n(rep1)" )

		(idr_pr_rep2, idr_pr_rep2_png) = _idr2( peak_pr1{"spp,2"}, peak_pr2{"spp,2"}, peak{"spp,2"}, idr_out_dir, "rep2_pr", \
					"peak_spp\\n(rep2_pr1)","peak_spp\\n(rep2_pr2)","peak_spp\\n(rep2)" )

		(idr_ppr, idr_ppr_png) = _idr2( peak_ppr1{"spp"}, peak_ppr2{"spp"}, peak_pooled{"spp"}, idr_out_dir, "ppr", \
					"peak_spp\\n(ppr1)","peak_spp\\n(ppr2)","peak_spp\\n(pooled)" )
		wait

		// get final idr qc score, use idr final idr narrow peak files from true, pseudo and pooled pseudo reps
		(idr_qc, idr_opt, idr_consv) = _idr_final_qc( idr_tr, idr_pr_rep1, idr_pr_rep2, idr_ppr, idr_out_dir, "" )
	}

	wait
}

void report() { // HTML chipseq report

	wait
	
	sys cp $script_dir/html/jquery.treetable.* $out_dir    # move report js/css files to out_dir

	report_file 	:= "$out_dir/Report.html"
	report_header 	:= "$script_dir/html/rpt_header.html"

	html := report_header.read()

	html += _html_chipseq_files() 	// treeview for directory and file structure 
	html += _html_chipseq_tracks() 	// epigenome browser tracks
	html += _html_chipseq_graphviz()// graphviz workflow diagram
	html += _html_chipseq_QC()	// show QC tables and images

	html += "<br></body></html>"

	report_file.write( html )
}

string _html_chipseq_graphviz() {

	html := "<div id='chipseq_graphviz'><b>Workflow diagram</b>"

	html += _html_graphviz()

	html += "</div><br>\n"
	return html
}

string _html_chipseq_files() {

	html := "<div id='chipseq_files'><b>Directories and files</b>"

	html += "<table id='filetable'> <caption>"+\
		"<a href='#' onclick=\"jQuery('#filetable').treetable('expandAll'); return false;\">Expand all</a> &nbsp&nbsp" + \
		"<a href='#' onclick=\"jQuery('#filetable').treetable('collapseAll'); return false;\">Collapse all</a>" + \
		"</caption> <thead><tr><th>Files</th><th>Path</th></tr></thead> <tbody>"

	for ( int ctl=0; ctl <= num_ctl; ctl++) {

		for ( int rep=1; rep <= get_num_rep(); rep++) {

			if ( !is_data_available( ctl, rep ) ) continue

			info 	:= get_info( ctl, rep )
			key 	:= "$ctl,$rep"
			
			align_out_dir 	:= "$out_dir/align_$info"
			
			if ( ctl == 0 ) \
				html += "<tr data-tt-id='$key'><td>" + "Replicate $rep ($info)"+ "</td><td>"+ _html_link_url( align_out_dir ) +"</td></tr>"
			else \
				html += "<tr data-tt-id='$key'><td>" + "Control $rep ($info)" + "</td><td>"+ _html_link_url( align_out_dir )  +"</td></tr>"

			// replicates and controls
			if ( is_paired_end( ctl, rep ) ) {
				
				if ( fastq.hasKey(key+",1") ) {
					html += "<tr data-tt-id='fastq_$key"+"_1' data-tt-parent-id='$key'><td>" + "fastq pair 1" + "</td><td>"+_html_link_url( fastq{key+",1"} )+"</td></tr>"
					html += "<tr data-tt-id='fastq_$key"+"_2' data-tt-parent-id='$key'><td>" + "fastq pair 2" + "</td><td>"+_html_link_url( fastq{key+",2"} )+"</td></tr>"
				}
			}
			else {
				if ( fastq.hasKey(key) ) {
					html += "<tr data-tt-id='fastq_$key' data-tt-parent-id='$key'><td>" + "fastq" + "</td><td>"+_html_link_url( fastq{key} )+"</td></tr>"
				}
			}

			if ( bam.hasKey(key) ) 	    html += "<tr data-tt-id='bam_$key' data-tt-parent-id='$key'><td>" + "bam" + "</td><td>"+_html_link_url( bam{key} )+"</td></tr>"
			if ( filt_bam.hasKey(key) ) html += "<tr data-tt-id='filt_bam_$key' data-tt-parent-id='$key'><td>" + "filtered bam" + "</td><td>"+_html_link_url( filt_bam{key} )+"</td></tr>"
			if ( tag.hasKey(key) ) 	    html += "<tr data-tt-id='tag_$key' data-tt-parent-id='$key'><td>" + "tagalign" + "</td><td>"+_html_link_url( tag{key} )+"</td></tr>"
		}
	}

	// signal track

	if ( signal_trk.hasKey("0,1") || signal_trk_pval.hasKey("rep1") ) {

		html += "<tr data-tt-id='signal'><td>" + "Signal track"+ "</td><td>"+ "" +"</td></tr>"

		for ( int ctl=0; ctl <= num_ctl; ctl++) {
		
			for ( int rep=1; rep <= get_num_rep(); rep++) {

				if ( !is_data_available( 0, rep ) ) continue

				info 	:= get_info( ctl, rep )
				key 	:= "$ctl,$rep"

				if ( signal_trk.hasKey(key) ) html += "<tr data-tt-id='signal_$key' data-tt-parent-id='signal'><td>" + "pval ($info)" + "</td><td>"+ _html_link_url( signal_trk{key} )+"</td></tr>"
			}
		}		
		
		peak_out_dir := "$out_dir/peaks_macs2"

		// true rep
		html += "<tr data-tt-id='signal_trk_truerep' data-tt-parent-id='signal'><td>" + "True replicates"+ "</td><td>"+ _html_link_url( peak_out_dir+"/true_replicates" ) +"</td></tr>"

		for ( int rep=1; rep <= get_num_rep(); rep++) {

			if ( signal_trk_pval.hasKey("rep$rep") ) html += "<tr data-tt-id='signal_trk_$rep' data-tt-parent-id='signal_trk_truerep'><td>" + "pval (rep$rep)"+ "</td><td>"+ _html_link_url( signal_trk_pval{"rep$rep"} ) +"</td></tr>"
			if ( signal_trk_fc.hasKey("rep$rep") ) html += "<tr data-tt-id='signal_trk_$rep' data-tt-parent-id='signal_trk_truerep'><td>" + "fc (rep$rep)"+ "</td><td>"+ _html_link_url( signal_trk_fc{"rep$rep"} ) +"</td></tr>"
		}

		// pseudo rep
		if ( signal_trk_pval.hasKey("rep1_pr1") ) \
			html += "<tr data-tt-id='signal_trk_pr' data-tt-parent-id='signal'><td>" + "Pseudo replicates"+ "</td><td>"+ _html_link_url( peak_out_dir+"/pseudo_replicates" ) +"</td></tr>"

		for ( int rep=1; rep <= get_num_rep(); rep++) {
			if ( signal_trk_pval.hasKey("rep$rep"+"_pr1") ) html += "<tr data-tt-id='signal_trk_pr1_$rep' data-tt-parent-id='signal_trk_pr'><td>" + "pval (rep$rep pr1)"+ "</td><td>"+ _html_link_url( signal_trk_pval{"rep$rep"+"_pr1"} ) +"</td></tr>"
			if ( signal_trk_fc.hasKey("rep$rep"+"_pr1") ) html += "<tr data-tt-id='signal_trk_pr1_$rep' data-tt-parent-id='signal_trk_pr'><td>" + "fc (rep$rep pr1)"+ "</td><td>"+ _html_link_url( signal_trk_fc{"rep$rep"+"_pr1"} ) +"</td></tr>"
			if ( signal_trk_pval.hasKey("rep$rep"+"_pr2") ) html += "<tr data-tt-id='signal_trk_pr2_$rep' data-tt-parent-id='signal_trk_pr'><td>" + "pval (rep$rep pr2)"+ "</td><td>"+ _html_link_url( signal_trk_pval{"rep$rep"+"_pr2"} ) +"</td></tr>"
			if ( signal_trk_fc.hasKey("rep$rep"+"_pr2") ) html += "<tr data-tt-id='signal_trk_pr2_$rep' data-tt-parent-id='signal_trk_pr'><td>" + "fc (rep$rep pr2)"+ "</td><td>"+ _html_link_url( signal_trk_fc{"rep$rep"+"_pr2"} ) +"</td></tr>"
		}

		if ( signal_trk_pval.hasKey("ppr1") ) {
			html += "<tr data-tt-id='signal_trk_ppr' data-tt-parent-id='signal'><td>" + "Pooled pseudo replicates"+ "</td><td>"+ _html_link_url( peak_out_dir+"/pooled_pseudo_replicates" ) +"</td></tr>"
			html += "<tr data-tt-id='signal_trk_ppr1' data-tt-parent-id='signal_trk_ppr'><td>" + "pval (ppr1)"+ "</td><td>"+ _html_link_url( signal_trk_pval{"ppr1"} ) +"</td></tr>"
			html += "<tr data-tt-id='signal_trk_ppr1' data-tt-parent-id='signal_trk_ppr'><td>" + "fc (ppr1)"+ "</td><td>"+ _html_link_url( signal_trk_fc{"ppr1"} ) +"</td></tr>"
			html += "<tr data-tt-id='signal_trk_ppr2' data-tt-parent-id='signal_trk_ppr'><td>" + "pval (ppr2)"+ "</td><td>"+ _html_link_url( signal_trk_pval{"ppr2"} ) +"</td></tr>"
			html += "<tr data-tt-id='signal_trk_ppr2' data-tt-parent-id='signal_trk_ppr'><td>" + "fc (ppr2)"+ "</td><td>"+ _html_link_url( signal_trk_fc{"ppr2"} ) +"</td></tr>"
		}

	}

	// peaks

	if ( peak.hasKey("spp,1") ) { // if peak output exists

		peak_out_dir := "$out_dir/peaks_spp"

		html += "<tr data-tt-id='peak'><td>" + "Peaks"+ "</td><td>"+ _html_link_url( peak_out_dir ) +"</td></tr>"

		// true rep
		html += "<tr data-tt-id='peak_truerep' data-tt-parent-id='peak'><td>" + "True replicates"+ "</td><td>"+ _html_link_url( peak_out_dir+"/true_replicates" ) +"</td></tr>"

		for ( int rep=1; rep <= get_num_rep(); rep++) {

			if ( peak.hasKey("spp,$rep") ) html += "<tr data-tt-id='peak_$rep' data-tt-parent-id='peak_truerep'><td>" + "rep$rep"+ "</td><td>"+ _html_link_url( peak{"spp,$rep"} ) +"</td></tr>"
		}

		// pseudo rep
		if ( peak_pr1.hasKey("spp,1") ) \
			html += "<tr data-tt-id='peak_pr' data-tt-parent-id='peak'><td>" + "Pseudo replicates"+ "</td><td>"+ _html_link_url( peak_out_dir+"/pseudo_replicates" ) +"</td></tr>"

		for ( int rep=1; rep <= get_num_rep(); rep++) {
			if ( peak_pr1.hasKey("spp,$rep") ) html += "<tr data-tt-id='peak_pr1_$rep' data-tt-parent-id='peak_pr'><td>" + "rep$rep pr1"+ "</td><td>"+ _html_link_url( peak_pr1{"spp,$rep"} ) +"</td></tr>"
			if ( peak_pr2.hasKey("spp,$rep") ) html += "<tr data-tt-id='peak_pr2_$rep' data-tt-parent-id='peak_pr'><td>" + "rep$rep pr2"+ "</td><td>"+ _html_link_url( peak_pr2{"spp,$rep"} ) +"</td></tr>"
		}

		if ( peak_ppr1.hasKey("spp") ) {
			html += "<tr data-tt-id='peak_ppr' data-tt-parent-id='peak'><td>" + "Pooled pseudo replicates"+ "</td><td>"+ _html_link_url( peak_out_dir+"/pooled_pseudo_replicates" ) +"</td></tr>"

			html += "<tr data-tt-id='peak_ppr1' data-tt-parent-id='peak_ppr'><td>" + "ppr1"+ "</td><td>"+ _html_link_url( peak_ppr1{"spp"} ) +"</td></tr>"
			html += "<tr data-tt-id='peak_ppr2' data-tt-parent-id='peak_ppr'><td>" + "ppr2"+ "</td><td>"+ _html_link_url( peak_ppr2{"spp"} ) +"</td></tr>"
		}
	}

	// idr

	if ( idr_tr != "" ) { // if idr output exists

		string idr_out_dir

		if ( is_input_peak() ) idr_out_dir = "$out_dir/idr2"
		else 		       idr_out_dir = "$out_dir/peaks_spp/idr2"

		html += "<tr data-tt-id='idr'><td>" + "IDR peaks"+ "</td><td>"+ _html_link_url( idr_out_dir ) +"</td></tr>"

 		if ( idr_tr!="" ) html += "<tr data-tt-id='idr_tr' data-tt-parent-id='idr'><td>" + "True replicates"+ "</td><td>"+ _html_link_url( idr_tr ) +"</td></tr>"		
		if ( idr_pr_rep1!="" ) html += "<tr data-tt-id='idr_pr_rep1' data-tt-parent-id='idr'><td>" + "Pseudo-replicates (rep1)"+ "</td><td>"+ _html_link_url( idr_pr_rep1 ) +"</td></tr>"		
		if ( idr_pr_rep2!="" ) html += "<tr data-tt-id='idr_pr_rep2' data-tt-parent-id='idr'><td>" + "Pseudo-replicates (rep2)"+ "</td><td>"+ _html_link_url( idr_pr_rep2 ) +"</td></tr>"		
		if ( idr_ppr!="" ) html += "<tr data-tt-id='idr_ppr' data-tt-parent-id='idr'><td>" + "Pooled pseudo-replicates"+ "</td><td>"+ _html_link_url( idr_ppr ) +"</td></tr>"		
		if ( idr_opt!="" ) html += "<tr data-tt-id='idr_opt' data-tt-parent-id='idr'><td>" + "Optimal set"+ "</td><td>"+ _html_link_url( idr_opt ) +"</td></tr>"		
		if ( idr_consv!="" ) html += "<tr data-tt-id='idr_consv' data-tt-parent-id='idr'><td>" + "Conservative set"+ "</td><td>"+ _html_link_url( idr_consv ) +"</td></tr>"		
	}

	html += "</tbody></table>"
	html += "</div><br>\n"

	return html
}

string _html_chipseq_QC() {

	string[] flagstat_qcs, dup_qcs, flagstat_nodup_qcs, pbc_qcs, xcor_qcs, xcor_plots
	string[] flagstat_headers, dup_headers, flagstat_nodup_headers, pbc_headers, xcor_headers

	for ( int ctl=0; ctl <= num_ctl; ctl++) { // iterate through inputs (ctl==0 : replicate, ctl==1 : control)	
	
		for ( int rep=1; rep <= get_num_rep(); rep++) {

			if ( !is_data_available( ctl, rep ) ) continue

			info := get_info( ctl, rep )
			key := "$ctl,$rep"

			if ( ctl == 0 ) {
				//html_rep_by_id += _html_xcor( info, 	[ info ], [ xcor_qc{key} ], [ xcor_plot{key} ], [ info ] )

				if ( xcor_qc.hasKey( key ) )	{
					xcor_qcs 		+= xcor_qc{key}
					xcor_plots 		+= xcor_plot{key}

					xcor_headers 		+= info
				}
			}

			if ( flagstat_qc.hasKey( key ) ) 	flagstat_qcs 		+= flagstat_qc{key}
			if ( dup_qc.hasKey( key ) ) 		dup_qcs 		+= dup_qc{key}
			if ( flagstat_nodup_qc.hasKey( key ) )	flagstat_nodup_qcs 	+= flagstat_nodup_qc{key}
			if ( pbc_qc.hasKey( key ) ) 		pbc_qcs			+= pbc_qc{key}

			if ( flagstat_qc.hasKey( key ) ) 	flagstat_headers 	+= info
			if ( dup_qc.hasKey( key ) ) 		dup_headers 		+= info
			if ( flagstat_nodup_qc.hasKey( key ) )	flagstat_nodup_headers 	+= info
			if ( pbc_qc.hasKey( key ) ) 		pbc_headers		+= info

		}
	}	

	html := "<div id='chipseq_qc'>"
	
	html += _parse_flagstat_to_html( "all", 	flagstat_headers, flagstat_qcs, flagstat_headers )
	html += _parse_dup_to_html( "all", 		dup_headers, dup_qcs, dup_headers )
	html += _parse_flagstat_to_html( "all, filtered",flagstat_nodup_headers, flagstat_nodup_qcs, flagstat_nodup_headers )
	html += _parse_pbc_to_html( "all", 		pbc_headers, pbc_qcs, pbc_headers )
	html += _parse_xcor_to_html( "all", 		xcor_headers, xcor_qcs, xcor_plots, xcor_headers )

	// if idr qc's exists, add them to html

	if ( idr_qc != "" ) html += _parse_idr_to_html( "idr", idr_qc )

	if ( idr_tr_png != "" ) html += _html_img( idr_tr_png, 800, "Nt" ) + "&nbsp"
	if ( idr_pr_rep1_png != "" ) html += _html_img( idr_pr_rep1_png, 800, "N1" ) + "&nbsp"
	if ( idr_pr_rep2_png != "" ) html += _html_img( idr_pr_rep2_png, 800, "N2" ) + "&nbsp"
	if ( idr_ppr_png != "" ) html += _html_img( idr_ppr_png, 800, "Np" ) + "&nbsp"

	html += "</div><br>"
	return html
}

string _html_chipseq_tracks() {

	string[] track_files, track_types, track_names

	if ( signal_trk_pval.hasKey( "pooled" ) ) { track_types += "bigwig"; track_names += "pval (pooled)"; track_files += signal_trk_pval{"pooled"} }
	if ( signal_trk_fc.hasKey( "pooled" ) )   { track_types += "bigwig"; track_names += "fc (pooled)";   track_files += signal_trk_fc{"pooled"}   }
	if ( idr_opt != "" ) {	track_types += "hammock"; track_names += "peak idr (opt. set)"; track_files += _peak_to_hammock( idr_opt ) }
	if ( idr_consv != "" ) {track_types += "hammock"; track_names += "peak idr (cons. set)"; track_files += _peak_to_hammock( idr_consv ) }

	for (int rep=1; rep<=get_num_rep(); rep++) {

		if ( signal_trk_pval.hasKey( "rep$rep" ) ) { track_types += "bigwig"; track_names += "pval (rep$rep)"; track_files += signal_trk_pval{"rep$rep"} }
		if ( signal_trk_fc.hasKey( "rep$rep" ) )   { track_types += "bigwig"; track_names += "fc (rep$rep)";   track_files += signal_trk_fc{"rep$rep"}   }
		if ( rep==1 && idr_pr_rep1 != "" ) {	track_types += "hammock"; track_names += "peak idr (rep1_pr)"; track_files += _peak_to_hammock( idr_pr_rep1 ) }
		if ( rep==2 && idr_pr_rep2 != "" ) {	track_types += "hammock"; track_names += "peak idr (rep2_pr)"; track_files += _peak_to_hammock( idr_pr_rep2 ) }
	}
	/*
	if ( idr_tr != "" ) {	track_types += "hammock"; track_names += "peak idr (true)"; track_files += _peak_to_hammock( idr_tr ) }
	if ( idr_pr_rep1 != "" ) {	track_types += "hammock"; track_names += "peak idr (pr1)"; track_files += _peak_to_hammock( idr_pr_rep1 ) }
	if ( idr_pr_rep2 != "" ) {	track_types += "hammock"; track_names += "peak idr (pr2)"; track_files += _peak_to_hammock( idr_pr_rep2 ) }
	if ( idr_ppr != "" ) {	track_types += "hammock"; track_names += "peak idr (ppr)"; track_files += _peak_to_hammock( idr_ppr ) }

	// add peak tracks
	for (int rep=1; rep<=get_num_rep(); rep++) {
		if ( peak.hasKey( "spp,$rep" ) ) { 	track_types += "hammock"; track_names += "peak spp (rep$rep)"; track_files += _peak_to_hammock( peak{"spp,$rep"} ) }
		if ( peak_pr1.hasKey( "spp,$rep" ) ) { track_types += "hammock"; track_names += "peak (rep$rep pr1)"; track_files += _peak_to_hammock( peak_pr1{"spp,$rep"} ) }
		if ( peak_pr2.hasKey( "spp,$rep" ) ) { track_types += "hammock"; track_names += "peak (rep$rep pr2)"; track_files += _peak_to_hammock( peak_pr2{"spp,$rep"} ) }
	}

	if ( peak_pooled.hasKey("spp") ) {	track_types += "hammock"; track_names += "peak (pooled)"; track_files += _peak_to_hammock( peak_pooled{"spp"} ) }
	if ( peak_ppr1.hasKey("spp") ) {	track_types += "hammock"; track_names += "peak (ppr1)"; track_files += _peak_to_hammock( peak_ppr1{"spp"} ) }
	if ( peak_ppr2.hasKey("spp") ) {	track_types += "hammock"; track_names += "peak (ppr2)"; track_files += _peak_to_hammock( peak_ppr2{"spp"} ) }

	// add signal tracks
	for (int rep=1; rep<=get_num_rep(); rep++) {

		key := "0,$rep"
		if ( signal_trk.hasKey( key ) ) { 	track_types += "bigwig"; track_names += "pval (rep$rep)"; track_files += signal_trk{key} }
	}
	*/

	html := _html_epg_browser_viz( track_files, track_types, track_names )

	return html
}


void help() {

	if ( is_cmd_line_arg_empty() ) {
		printHelp()
		exit
	}
}

// basic functions

bool is_callpeak_spp() {

	return callpeak.toLower().indexOf("spp")>=0
}

bool is_callpeak_macs2() {

	return callpeak.toLower().indexOf("macs")>=0
}

bool is_callpeak_gem() {

	return callpeak.toLower().indexOf("gem")>=0
}

bool is_sigtrk_aln2rawsig() {

	return sigtrk.toLower().startsWith( "aln" ) || sigtrk.toLower().startsWith( "align" ) || sigtrk.toLower().startsWith( "tag" )
}

bool is_sigtrk_deeptools() {

	return sigtrk.toLower().startsWith( "deep" ) || sigtrk.toLower().startsWith( "bam" )
}

bool is_align_only_mode() {

	return !is_input_peak() && !control_exists() && !is_callpeak_macs2()
}

bool is_input_fastq() {

	return input.toLower() == "fastq"
}

bool is_input_bam() {

	return input.toLower() == "bam"
}

bool is_input_filt_bam() {

	return input.toLower() == "filt_bam"
}

bool is_input_tag() {

	return input.toLower() == "tag" || input.toLower() == "tagalign"
}

bool is_input_peak() {

	return input.toLower() == "peak"
}

int get_num_rep() {

	if ( is_input_fastq() ) 	 return get_num_rep_fastq()
	else if ( is_input_bam() )  	 return get_num_rep_bam()
	else if ( is_input_filt_bam() )  return get_num_rep_filt_bam()
	else if ( is_input_tag() ) 	 return get_num_rep_tag()
	else if ( is_input_peak() ) 	 return get_num_rep_peak()

	return 0
}

bool is_final_stage_bam() {

	return final_stage.toLower() == "bam"
}

bool is_final_stage_filt_bam() {

	return final_stage.toLower() == "filt_bam"
}

bool is_final_stage_tag() {

	return final_stage.toLower() == "tag" || final_stage.toLower() == "tagalign"
}

bool is_final_stage_xcor() {

	return final_stage.toLower() == "xcor"
}

bool is_final_stage_peak() {

	return final_stage.toLower() == "peak"
}

bool is_final_stage_idr() {

	return (final_stage.toLower() == "idr") || (final_stage.toLower() == "")
}

bool is_paired_end( int ctl, int rep ) {

	if ( is_input_fastq() ) {

		fastqs := get_fastqs( ctl, rep )
		return fastqs.size()==2
	}
	else {
		return pe
	}
}

bool is_single_ended( int ctl, int rep ) {

	return !is_paired_end( ctl, rep )
}

bool is_data_available( int ctl, int rep ) {

	if ( (ctl==1) && (rep>2) ) return false

	if ( is_input_fastq() ) {
		return get_fastqs( ctl, rep ).size() > 0
	}
	else if ( is_input_bam() ) {
		return get_bam( ctl, rep ) != ""
	}
	else if ( is_input_filt_bam() ) {
		return get_filt_bam( ctl, rep ) != ""
	}
	else if ( is_input_tag() ) {
		return get_tag( ctl, rep ) != ""
	}
	else {
		return false
	}
}

bool control_exists() {
	return is_data_available( 1, 1 )
}

string get_info( int ctl, int rep ) {

	return ( (ctl>0) ? "ctl" : "rep") + rep
}
