#!/usr/bin/env bds
#vim: syntax=java

include "species.bds"
include "report_graphviz.bds"


help == align bowtie2 settings (requirements: -bwt2_idx)
//help *resources :
nth_bwt2	:= 2 			help # threads for bowtie2 (default: 2).
wt_bwt2		:= "23h"		help Walltime for bowtie2 (default: 23h, 23:00:00).
mem_bwt2	:= "12G"	 	help Max. memory for bowtie2 (default: 8G).


init_align_bwt2()


void init_align_bwt2() {

	nth_bwt2 	= get_conf_val_int( nth_bwt2,		["nth_bwt2"] )
	wt_bwt2 	= get_conf_val( wt_bwt2, 		["wt_bwt2"] )
	mem_bwt2 	= get_conf_val( mem_bwt2, 		["mem_bwt2"] )

	print("\n\n== align bowtie2 settings\n")
	print( "# threads (bowtie2)\t: $nth_bwt2\n")
	print( "Walltime (bowtie2)\t: $wt_bwt2\n")
	print( "Max. memory (bowtie2)\t: $mem_bwt2\n")
}

void chk_align_bwt2() {

	if ( !path_exists("$bwt2_idx.1.bt2") ) error("Bowtie2 index (-bwt2_idx) doesn't exists! (file: $bwt2_idx.1.bt2)\n")
}

string[] _bowtie2( string fastq, string o_dir, string info ) {

	prefix 	:= replace_dir( rm_ext( fastq, ["fastq","fq"] ), o_dir )
	bam 	:= "$prefix.bam"
	log 	:= "$prefix.align.log"

	in 	:= [ fastq ]
	out 	:= [ bam, log ]

	taskName:= "bowtie2 "+info
	cpus 	:= nth_bwt2; 	mem := get_res_mem(mem_bwt2); 	timeout := get_res_wt(wt_bwt2)

	task( out<-in ) {

		sys $shcmd_init

		//sys bowtie2 -x $bwt2_idx --threads $nth_bwt2 -U <(zcat -f $fastq) -S /dev/stdout 2> $log | \
		//	samtools view -o $bam -b /dev/stdin
		sys bowtie2 -x $bwt2_idx --threads $nth_bwt2 -U <(zcat -f $fastq) 2> $log | \
			samtools view -bS - > $bam
	}

	wait_par()

	return out
}

string[] _bowtie2_PE( string fastq1, string fastq2, string o_dir, string info ) {

	prefix 	:= replace_dir( rm_ext( fastq1, ["fastq","fq"] ), o_dir ) + ".PE2SE"
	bam 	:= "$prefix.bam"
	log 	:= "$prefix.align.log"

	in 	:= [ fastq1, fastq2 ]
	out 	:= [ bam, log ]

	taskName:= "bowtie2_PE "+info
	cpus 	:= nth_bwt2; 	mem := get_res_mem(mem_bwt2); 	timeout := get_res_wt(wt_bwt2)

	task( out<-in ) {

		sys $shcmd_init

		sys bowtie2 -X2000 --mm --threads $nth_bwt2 -x $bwt2_idx \
			-1 $fastq1 -2 $fastq2 \
			2>$log | \
			samtools view -bS - > $bam
	}

	wait_par()

	return out
}
