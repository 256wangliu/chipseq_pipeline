#!/usr/bin/env bds
#vim: syntax=java

include "species.bds"
include "report_graphviz.bds"

string _trim_adapters( string fastq, string o_dir, string label ) {

	return _trim_adapters( fastq, o_dir, label, "fastq\\n($label)", "trimmed\\nfastq\\n($label)" )
}

string _trim_adapters( string fastq, string o_dir, string label, string label_in, string label_out ) {

	prefix	:= replace_dir( rm_ext( fastq, ["fastq","fq"] ), o_dir )
	p 	:= "$prefix"+"_trimmed.fq"
	p_gz	:= "$p.gz"
	in 	:= [ fastq ]
	out 	:= p_gz

	taskName:= "trim_adapters " + label

	task( out<-in ) {

		sys $shcmd_init

		sys trim_galore $fastq -o $o_dir
		sys if [ ! -f $p_gz ]; then gzip $p; fi
	}

	_add_to_graphviz( [label_in], [fastq], [label_out], [p_gz], "trim_galore\\n($label)", grp_color_trim_adapter )


	wait_par()

	return out
}

string[] _trim_adapters_PE( string fastq1, string fastq2, string o_dir, string label ) {

	return _trim_adapters_PE( fastq1, fastq2, o_dir, label, \
				"fastq1\\n($label)", "fastq2\\n($label)", \
				"trimmed\\nfastq1\\n($label)", "trimmed\\nfastq2\\n($label)" )
}

string[] _trim_adapters_PE( string fastq1, string fastq2, string o_dir, string label, \
			string label_in1, string label_in2, string label_out1, string label_out2 ) {

	prefix1	:= replace_dir( rm_ext( fastq1, ["fastq","fq"] ), o_dir )
	prefix2	:= replace_dir( rm_ext( fastq2, ["fastq","fq"] ), o_dir )
	p1 	:= "$prefix1.trim.fastq"
	p2 	:= "$prefix2.trim.fastq"
	p1_gz	:= "$p1.gz"
	p2_gz 	:= "$p2.gz"

	in 	:= [ fastq1, fastq2 ]
	out 	:= [ p1_gz, p2_gz ]

	taskName:= "trim_adapters_PE " + label

	task( out<-in ) {

		sys $shcmd_init

		sys cd $o_dir
		sys python $(which trimAdapters.py) -a $fastq1 -b $fastq2
		sys gzip $p1
		sys gzip $p2
	}

	_add_to_graphviz( [label_in1,label_in2], [fastq1,fastq2], [label_out1,label_out2], [p1_gz,p2_gz], \
				"trim_adapter\\n($label)", grp_color_trim_adapter )

	wait_par()

	return out
}
