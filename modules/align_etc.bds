#!/usr/bin/env bds
#vim: syntax=java

include "species.bds"
include "report.bds"


string _trim_adapters( string fastq, string o_dir, string label ) {

	return _trim_adapters( fastq, o_dir, label, \
				"fastq_($label)", "trimmed_fastq_($label)", "L1_align/$label/trimmed_fastq" )
}

string _trim_adapters( string fastq, string o_dir, string label, \
			string graph_in, string graph_out, string hrchy_out ) {

	prefix	:= replace_dir( rm_ext( fastq, ["fastq","fq"] ), o_dir )
	p 	:= "$prefix"+"_trimmed.fq"
	p_gz	:= "$p.gz"
	p2 	:= "$prefix"+"_trimmed.fastq"
	p2_gz 	:= "$p2.gz"
	in 	:= [ fastq ]
	out 	:= p2_gz

	taskName:= "trim_adapters " + label

	task( out<-in ) {

		sys $shcmd_init

		sys trim_galore $fastq -o $o_dir
		sys if [ ! -f $p_gz ]; then gzip $p; fi
		sys mv $p_gz $p2_gz
		sys rm -f $p_gz $p
	}

	_add_to_graphviz( [graph_in], [fastq], [graph_out], [p2_gz], "trim_galore\\n($label)", grp_color_trim_adapter )

	_add_to_filetable( [hrchy_out], [p2_gz] )

	wait_par()

	return out
}

string[] _trim_adapters_PE( string fastq1, string fastq2, string o_dir, string label ) {

	return _trim_adapters_PE( fastq1, fastq2, o_dir, label, \
				"fastq1_($label)", "fastq2_($label)", "trimmed_fastq1_($label)", "trimmed_fastq2_($label)",  \
				"L1_align/$label/trimmed_fastq1", "L1_align/$label/trimmed_fastq2" )
}

string[] _trim_adapters_PE( string fastq1, string fastq2, string o_dir, string label, \
			string graph_in1, string graph_in2, string graph_out1, string graph_out2, \
			string hrchy_out1, string hrchy_out2 ) {

	prefix1	:= replace_dir( rm_ext( fastq1, ["fastq","fq"] ), o_dir )
	prefix2	:= replace_dir( rm_ext( fastq2, ["fastq","fq"] ), o_dir )
	p1 	:= "$prefix1.trim.fastq"
	p2 	:= "$prefix2.trim.fastq"
	p1_gz	:= "$p1.gz"
	p2_gz 	:= "$p2.gz"

	in 	:= [ fastq1, fastq2 ]
	out 	:= [ p1_gz, p2_gz ]

	taskName:= "trim_adapters_PE " + label

	task( out<-in ) {

		sys $shcmd_init

		sys cd $o_dir
		sys python $(which trimAdapters.py) -a $fastq1 -b $fastq2
		sys gzip $p1
		sys gzip $p2
	}

	_add_to_graphviz( [graph_in1,graph_in2], [fastq1,fastq2], [graph_out1,graph_out2], [p1_gz,p2_gz], \
				"trim_adapter\\n($label)", grp_color_trim_adapter )

	_add_to_filetable( [hrchy_out1,hrchy_out2], [p1_gz,p2_gz] )

	wait_par()

	return out
}
