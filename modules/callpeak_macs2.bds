#!/usr/bin/env bds
#vim: syntax=java

include "species.bds"
include "report.bds"


help == callpeak macs2 settings (requirements: -chrsz -gensz)

nth_macs2		:= 2 		help # threads for MACS2 (default: 2)
wt_macs2		:= "10h"	help Walltime for MACS2 (default: 10h, 10:00:00).
mem_macs2		:= "8G" 	help Max. memory for MACS2 (default: 8G).


init_callpeak_macs2()


void init_callpeak_macs2() {

	nth_macs2 		= get_conf_val_int( nth_macs2,		["nth_macs2"] )
	wt_macs2 		= get_conf_val( wt_macs2, 		["wt_macs2"] )
	mem_macs2 		= get_conf_val( mem_macs2, 		["mem_macs2"] )

	print("\n\n== callpeak macs2 settings\n")
	print( "# threads (macs2)\t\t: $nth_macs2\n")
	print( "Walltime (macs2)\t\t: $wt_macs2\n")
	print( "Max. memory (macs2)\t\t: $mem_macs2\n")
}

void chk_callpeak_macs2() {

	if ( gensz == "" ) error("Genome size (-gensz) must be defined! (mm for mouse, hs for human)")
	if ( !path_exists( chrsz ) ) error("Chromosome size file (-chrsz) is missing! (file: $chrsz)")
}


string[] _macs2( string tag, string ctl_tag, string fraglen, bool make_sig, \
		string peak_o_dir, string sig_o_dir, string label, string label_ctl, string hrchy ) {

	prefix 		:= (ctl_tag != "") ? 	("$peak_o_dir/" + make_x_basename_wo_gz( tag, ctl_tag, "" ) ) \
						: replace_dir( rm_ext( tag, "tagAlign" ), peak_o_dir )
	prefix_sig 	:= replace_dir( prefix, sig_o_dir )

	npeakfile	:= "$prefix.narrowPeak.gz"
	bpeakfile	:= "$prefix.broadPeak.gz"
	gpeakfile	:= "$prefix.gappedPeak.gz"
	fc_bedgraph 	:= "$prefix.fc.signal.bedgraph"
	fc_bedgraph_srt	:= "$prefix.fc.signal.srt.bedgraph"
	fc_bigwig 	:= "$prefix_sig.fc.signal.bw"
	pval_bedgraph 	:= "$prefix.pval.signal.bedgraph"
	pval_bedgraph_srt := "$prefix.pval.signal.srt.bedgraph"
	pval_bigwig 	:= "$prefix_sig.pval.signal.bw"
	prefix_basename	:= get_basename( prefix )
	// macs2 params with or w/o control
 	ctl 	:= (ctl_tag != "") ? "-c $ctl_tag" : ""
	sval_line := (ctl_tag != "") ?	"controlReads=$(zcat $ctl_tag | wc -l | awk '{printf \"%f\", $1/1000000}'); sval=$(echo \"${chipReads} ${controlReads}\" | awk '$1>$2{printf \"%f\",$2} $1<=$2{printf \"%f\",$1}')" : \
					"sval=$(echo \"${chipReads}\" | awk '{printf \"%f\",$1}')"

	in 	:= (ctl_tag != "") ? [ tag, ctl_tag ] : [ tag ]
 	out 	:= make_sig ? [ npeakfile, gpeakfile, fc_bigwig, pval_bigwig ] : [ npeakfile, gpeakfile ]

 	taskName:= "macs2 " + label
	cpus	:= nth_macs2;	mem := get_res_mem(mem_macs2);	timeout := get_res_wt(wt_macs2)

	wait_par( cpus )

	tid := task( out<-in ) {

		sys $shcmd_init
		sys LC_COLLATE=C
		
		//==========================================
		//# Generate narrow peaks and preliminary signal tracks
		//============================================
		sys macs2 callpeak -t $tag $ctl -f BED -n $peak_o_dir/$prefix_basename -g $gensz -p 0.01 --nomodel --shift 0 --extsize $fraglen --keep-dup all -B --SPMR

		//# Sort by Col8 in descending order and replace long peak names in Column 4 with Peak_<peakRank>
		sys sort -k 8gr,8gr "$prefix"_peaks.narrowPeak | awk 'BEGIN{OFS="\t"}{$4="Peak_"NR ; print $0}' | gzip -c > $npeakfile

		//# remove additional files
		sys rm -f "$prefix"_peaks.xls \
			"$prefix"_peaks.narrowPeak \
			"$prefix"_summits.bed

		//===========================================
		//# Generate Broad and Gapped Peaks
		//============================================
		sys macs2 callpeak -t $tag $ctl -f BED -n $peak_o_dir/$prefix_basename -g $gensz -p 0.01 --broad --nomodel --shift 0 --extsize $fraglen --keep-dup all

		//# Sort by Col8 (for broadPeak) or Col 14(for gappedPeak)  in descending order and replace long peak names in Column 4 with Peak_<peakRank>
		sys sort -k 8gr,8gr "$prefix"_peaks.broadPeak | awk 'BEGIN{OFS="\t"}{$4="Peak_"NR ; print $0}' | gzip -c > $bpeakfile
		sys sort -k 14gr,14gr "$prefix"_peaks.gappedPeak | awk 'BEGIN{OFS="\t"}{$4="Peak_"NR ; print $0}' | gzip -c > $gpeakfile

		//# remove additional files
		sys rm -f "$prefix"_peaks.xls \
		  "$prefix"_peaks.broadPeak \
		  "$prefix"_peaks.gappedPeak \
		  "$prefix"_summits.bed

		//===========================================
		//# For Fold enrichment signal tracks
		//============================================
		//# This file is a tab delimited file with 2 columns Col1 (chromosome name), Col2 (chromosome size in bp).
		//# Remove coordinates outside chromosome sizes (stupid MACS2 bug)
		//# Convert bedgraph to bigwig
		//===========================================
		//# For -log10(p-value) signal tracks
		//============================================
		//# Compute sval = min(no. of reads in ChIP, no. of reads in control) / 1,000,000
		//# Remove coordinates outside chromosome sizes (stupid MACS2 bug)
		//# Convert bedgraph to bigwig

		sys if [[ $make_sig == "true" ]]; then \
			\
			macs2 bdgcmp -t "$prefix"_treat_pileup.bdg -c "$prefix"_control_lambda.bdg --outdir $peak_o_dir -o "$prefix_basename"_FE.bdg -m FE; \
			slopBed -i "$prefix"_FE.bdg -g $chrsz -b 0 |   awk '{if ($3 != -1) print $0}' |  bedClip stdin $chrsz $fc_bedgraph; \
			rm -f "$prefix"_FE.bdg; \
			\
			sort -k1,1 -k2,2n $fc_bedgraph > $fc_bedgraph_srt; \
			bedGraphToBigWig $fc_bedgraph_srt $chrsz $fc_bigwig; \
			rm -f $fc_bedgraph $fc_bedgraph_srt; \
			\
			chipReads=$(zcat $tag | wc -l | awk '{printf "%f", $1/1000000}'); \
			$sval_line; \
			macs2 bdgcmp -t "$prefix"_treat_pileup.bdg -c "$prefix"_control_lambda.bdg --outdir $peak_o_dir -o "$prefix_basename"_ppois.bdg -m ppois -S "${sval}"; \
			slopBed -i "$prefix"_ppois.bdg -g $chrsz -b 0 |   awk '{if ($3 != -1) print $0}' |  bedClip stdin $chrsz $peak_o_dir/$prefix_basename.pval.signal.bedgraph; \
			rm -rf "$prefix"_ppois.bdg; \
			\
			sort -k1,1 -k2,2n $pval_bedgraph > $pval_bedgraph_srt; \
			bedGraphToBigWig $pval_bedgraph_srt $chrsz $pval_bigwig; \
			\
			rm -f $pval_bedgraph $pval_bedgraph_srt; \
			fi

		sys rm -f "$prefix"_treat_pileup.bdg "$prefix"_control_lambda.bdg
	}

	register_par( tid, cpus )

	graph_in  := [ "tagalign_($label)","tagalign_($label_ctl)"]
	graph_out := [	"peak_macs2_($label)",\
			"gpeak_macs2_($label)",\
		      	"p-val_sig._($label)",\
		      	"fc_sig._($label)" ]
	hrchy_out := [	"L1_peak/macs2/$hrchy/peak",\
			"L1_peak/macs2/$hrchy/gpeak",\
		   	"L1_signal/macs2/$hrchy/pval", \
		   	"L1_signal/macs2/$hrchy/fc" ]

	_add_to_graphviz( graph_in, in, graph_out, out, "macs2_($label)", grp_color_macs2 )
	_add_to_filetable( hrchy_out, out )

	return out
}

string[] _macs2_atac( string tag, string smooth_window, string pval_thresh, bool make_sig, \
			string peak_o_dir, string sig_o_dir, \
			string label, string hrchy ) {

	pval_str 	:= (pval_thresh=="0.01") ? "" : ".pval$pval_thresh"	
	prefix 		:= replace_dir( rm_ext( tag, ["bed","tagAlign"] ), peak_o_dir ) + ".pf$pval_str"
	prefix_sig 	:= replace_dir( prefix, sig_o_dir )
	
	// peaks

	peakfile 	:= "$prefix.narrowPeak.gz"
	bpeakfile	:= "$prefix.broadPeak.gz"
	gpeakfile	:= "$prefix.gappedPeak.gz"

	fc_bedgraph 	:= "$prefix.fc.signal.bedgraph"
	fc_bedgraph_srt	:= "$prefix.fc.signal.srt.bedgraph"	
	fc_bigwig 	:= "$prefix_sig.fc.signal.bigwig"

	pval_bedgraph 	:= "$prefix.pval.signal.bedgraph"
	pval_bedgraph_srt 	:= "$prefix.pval.signal.srt.bedgraph"	
	pval_bigwig 	:= "$prefix_sig.pval.signal.bigwig"
 
	shiftsize := round( smooth_window.parseReal()/2.0 )

	in 	:= [ tag ]
	out 	:= make_sig ? [ peakfile, gpeakfile, fc_bigwig, pval_bigwig ] : [ peakfile, gpeakfile ]

	taskName:= "macs2_atac " + label + pval_str
	cpus	:= nth_macs2;	mem := get_res_mem(mem_macs2);	timeout := get_res_wt(wt_macs2)

	wait_par( cpus )

	tid := task( out<-in ) {
	
		sys $shcmd_init
		sys LC_COLLATE=C

		sys macs2 callpeak \
			-t $tag -f BED -n "$prefix" -g "$gensz" -p $pval_thresh \
			--nomodel --shift -$shiftsize --extsize $smooth_window --broad --keep-dup all

		//# Sort by Col8 in descending order and replace long peak names in Column 4 with Peak_<peakRank>
		sys sort -k 8gr,8gr "$prefix"_peaks.broadPeak | awk 'BEGIN{OFS="\t"}{$4="Peak_"NR ; print $0}' | gzip -c > $bpeakfile
		sys sort -k 14gr,14gr "$prefix"_peaks.gappedPeak | awk 'BEGIN{OFS="\t"}{$4="Peak_"NR ; print $0}' | gzip -c > $gpeakfile
		sys rm -f "$prefix"_peaks.broadPeak
		sys rm -f "$prefix"_peaks.gappedPeak

		sys macs2 callpeak \
			-t $tag -f BED -n "$prefix" -g "$gensz" -p $pval_thresh \
			--nomodel --shift -$shiftsize --extsize $smooth_window -B --SPMR --keep-dup all --call-summits

		//# Sort by Col8 in descending order and replace long peak names in Column 4 with Peak_<peakRank>
		sys sort -k 8gr,8gr "$prefix"_peaks.narrowPeak | awk 'BEGIN{OFS="\t"}{$4="Peak_"NR ; print $0}' | gzip -c > $peakfile
		sys rm -f "$prefix"_peaks.narrowPeak
		sys rm -f "$prefix"_peaks.xls
		sys rm -f "$prefix"_summits.bed

		sys if [[ $make_sig == "true" ]]; then \
			\
			macs2 bdgcmp -t "$prefix"_treat_pileup.bdg -c "$prefix"_control_lambda.bdg \
				--o-prefix "$prefix" -m FE;\
			slopBed -i "$prefix"_FE.bdg -g "$chrsz" -b 0 | bedClip stdin "$chrsz" $fc_bedgraph; \
			rm -f "$prefix"_FE.bdg; \
			\
			sort -k1,1 -k2,2n $fc_bedgraph > $fc_bedgraph_srt; \
			bedGraphToBigWig $fc_bedgraph_srt "$chrsz" "$fc_bigwig"; \
			rm -f $fc_bedgraph $fc_bedgraph_srt; \
			\
			# sval counts the number of tags per million in the (compressed) BED file; \
			sval=$(wc -l <(zcat -f "$tag") | awk '{printf "%f", $1/1000000}'); \
			\
			macs2 bdgcmp \
				-t "$prefix"_treat_pileup.bdg -c "$prefix"_control_lambda.bdg \
				--o-prefix "$prefix" -m ppois -S "${sval}"; \
			slopBed -i "$prefix"_ppois.bdg -g "$chrsz" -b 0 | bedClip stdin "$chrsz" $pval_bedgraph; \
			rm -f "$prefix"_ppois.bdg; \
			\
			sort -k1,1 -k2,2n $pval_bedgraph > $pval_bedgraph_srt; \
			bedGraphToBigWig $pval_bedgraph_srt "$chrsz" "$pval_bigwig"; \
			rm -f $pval_bedgraph $pval_bedgraph_srt; \
			\
			fi

		sys rm -f "$prefix"_treat_pileup.bdg "$prefix"_control_lambda.bdg
	}

	register_par( tid, cpus )

	// graphviz diagram and filetable

	suffix	:= (pval_thresh=="0.01") ? "0.01_" : ""
	graph_in  := ["tagalign_($label)"]
	graph_out := [	"peak_macs2_$suffix($label)",\
			"gpeak_macs2_$suffix($label)",\
		      	"p-val_sig._$suffix($label)",\
		      	"fc_sig._$suffix($label)" ]
	suffix2	:= (pval_thresh=="0.01") ? ",0.01" : ""
	hrchy_out := [	"L1_peak/macs2/$hrchy/peak$suffix2",\
			"L1_peak/macs2/$hrchy/gpeak$suffix2",\
		   	"L1_signal/macs2/$hrchy/pval$suffix2", \
		   	"L1_signal/macs2/$hrchy/fc$suffix2" ]
	box 	:= "macs2_p-val-thld._$pval_thresh"+"_($label)"	

	_add_to_graphviz( graph_in, in, graph_out, out, box, grp_color_macs2 )
	_add_to_filetable( hrchy_out, out )

	return out
}
