#!/usr/bin/env bds

include "species.bds"


help == callpeak spp settings
npeak 		:= 300000 	help Threshold on # peaks (-npeak in spp) (default: 300000).
//help *resources :
nth_spp 	:= 2		help # threads for spp (run_spp.R) (default: 2).
wt_spp		:= "15h"	help Walltime for spp (default: 10h, 10:00:00).
mem_spp		:= "8G" 	help Max. memory for spp (default: 8G).


init_callpeak_spp()


void init_callpeak_spp() {

	if ( conf_file_exists() ) {

		npeak 		= get_conf_val_int( npeak,		["npeak"] )
		nth_spp 	= get_conf_val_int( nth_spp,		["nthreads_spp", "nth_spp", "cpu_spp"] )
		wt_spp 		= get_conf_val( wt_spp, 		["walltime_spp", "wt_spp", "timeout_spp"] )
		mem_spp 	= get_conf_val( mem_spp, 		["memory_spp", "mem_spp"] )
	}

	print("\n\n== callpeak spp settings\n")
	print( "Threshold for # peak\t\t: $npeak\n")
	print( "# threads (spp)\t\t: $nth_spp\n")
	print( "Walltime (spp)\t\t: $wt_spp\n")
	print( "Max. memory (spp)\t: $mem_spp\n")
}

string[] _spp( string tag, string ctl_tag, string fraglen, string out_dir, string info ) {

	prefix_vs	:= "$out_dir/" + make_vs_basename_wo_gz( tag, ctl_tag, "" )
	prefix_x	:= "$out_dir/" + make_x_basename_wo_gz( tag, ctl_tag, "" )

	npeakfile_vs 	:= "$prefix_vs.narrowPeak.gz"
	rpeakfile_vs 	:= "$prefix_vs.regionPeak.gz"
	
	npeakfile 	:= "$prefix_x.narrowPeak.gz"
	rpeakfile 	:= "$prefix_x.regionPeak.gz"
	ccscore		:= "$prefix_x.ccscore"

	pdf_tmp		:= change_dir( remove_ext( tag, ["gz"] ), out_dir ) + ".pdf"
	pdf 		:= "$prefix_x.pdf"

	in 	:= [ tag, ctl_tag ]
	out 	:= [ npeakfile, rpeakfile, ccscore, pdf ]

	task( taskName:= "spp "+info, \
		cpus:=nth_spp, mem:=parse_mem_or_skip(mem_spp), timeout:=parse_time_or_skip(wt_spp), \
		out<-in ) {

		sys $SHCMD_INIT

		sys Rscript $(which run_spp_nodups.R) -c=$tag -p=$nth_spp -i=$ctl_tag \
			-npeak=$npeak -odir=$out_dir -speak=$fraglen -savn -savr -savp -rf -out=$ccscore

		// Bug fix (we have scientific representation of chr coord., possible bug in run_spp.R?):
		sys zcat $npeakfile_vs | awk 'BEGIN{OFS="\t"}{print $1,int($2),int($3),$4,$5,$6,$7,$8,$9,$10;}' | gzip -f -c > $npeakfile
		sys zcat $rpeakfile_vs | awk 'BEGIN{OFS="\t"}{print $1,int($2),int($3),$4,$5,$6,$7,$8,$9,$10;}' | gzip -f -c > $rpeakfile
		
		sys rm -f $npeakfile_vs
		sys rm -f $rpeakfile_vs

		sys mv $pdf_tmp $pdf
	}

	wait_par()

	return out
}
