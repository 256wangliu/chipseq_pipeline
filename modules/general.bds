#!/usr/bin/env bds
#vim: syntax=java

include "conf.bds"


help == general pipeline settings
out_dir 	:= "out"	help Output directory (default: out).
title		:= ""		help Prefix for HTML report and outputs without given prefix.

help == parallelization settings
no_par		:= false	help Serialize all jobs (identical to '-par_lvl 0', individual job can still use multiple threads).
par_lvl		:= 3		help Level of parallelization (Max # of replicates / Max # of peak-calling jobs; 0: no par., 1: 1/1, 2: 2/1, 3: 2/2, 4: 4/2, 5: 4/4, 6: custom, 7: unlimited, default: 3).
reps_in_par 	:= 2 		help Max # of replicates/controls to be processed in parallel (works with '-par_lvl 6' only, default: 2).
peaks_in_par 	:= 2 		help Max # of peak-calling jobs in parallel ((works with '-par_lvl 6' only, default: 2).

help == general resource settings
wt 		:= "11h"	help Default Walltime for cluster jobs (example: 8:10:00, 3h, 3600, default: 11h, 11:00:00).
memory 		:= "8G"		help Default Maximum memory for cluster jobs (identical to '-mem', example: 4.5G, 1024MB, default: 8G).
use_sys_default	:= false 	help Use system default resource settings (unlimited in most cases).

help == shell environment settings
mod 		:= ""		help Modules separated by ; (example: "bowtie/2.2.4; bwa/0.7.7; picard-tools/1.92").
shcmd 		:= ""		help Shell commands separated by ;. Shell var. must be written as ${VAR} not as $VAR (example: "export PATH=${PATH}:/usr/test; VAR=test"). 
addpath 	:= "" 		help Path separated by ; or : to be PREPENDED to \$PATH (example: "/bin/test:${HOME}/utils").
conda_env 	:= ""		help Anaconda Python environment name for all softwares including Python2.
conda_env_r2 	:= ""		help Anaconda Python environment name for R-2.x. Skip it if R-2.x is already installed on your Python2 conda env. ('-conda_env').
conda_env_py3 	:= ""		help Anaconda Python environment name for Python3.


shcmd_init 	:= ""  // Shell command executed prior to all BigDataScript tasks (use this for setting up shell environment)
shcmd_init_r2 	:= ""  // for softwares using R 2.x
shcmd_init_py3	:= ""  // for softwares using python3

string[] tids_rep
string[] tids_callpeak	// array of task ids for peak calling jobs


init_general()
init_shell_env()


void init_general() { 

	out_dir 	= get_conf_val( out_dir, 		["out_dir"] )		
	no_par 		= get_conf_val_bool( no_par, 		["no_par"] )
	par_lvl 	= get_conf_val_int( par_lvl, 		["par_lvl"] )
	reps_in_par 	= get_conf_val_int( reps_in_par, 	["reps_in_par"] )
	peaks_in_par 	= get_conf_val_int( peaks_in_par, 	["peaks_in_par"] )
	wt 		= get_conf_val( wt, 			["wt"] )
	memory 		= get_conf_val( memory, 		["memory","mem"] )
	use_sys_default	= get_conf_val_bool( use_sys_default, 	["use_sys_default"] )
	conda_env	= get_conf_val( conda_env, 		["conda_env"] )
	conda_env_r2	= get_conf_val( conda_env_r2, 		["conda_env_r2"] )
	conda_env_py3	= get_conf_val( conda_env_py3, 		["conda_env_py3"] )

	if ( cmd_line_arg_has_key("mem") ) memory = get_cmd_line_arg_val( "mem" )

	init_par()
	print_general()

	// cpus, mem and timeout are BDS variables for default resource settings
	cpus 		= 1
	mem 		= get_res_mem(memory)
	timeout 	= get_res_wt(wt)
	walltimeout 	= 3600*24*100 	// timeout var. in BigDataScript (100 days, jobs will never be stopped by BDS due to BDS timeout)
}

void init_par() {

	if ( no_par ) 	par_lvl = 0

	// set max. # of reps and peaks to be processed in parallel

	if ( par_lvl >= 7 ) { // unlimited
		reps_in_par 	= 100
		peaks_in_par 	= 100
	}	
	else if ( par_lvl >= 6 ) { // custom
		// vars are already read
	}
	else if ( par_lvl >= 5 ) {
		reps_in_par	= 4
		peaks_in_par 	= 4
	}
	else if ( par_lvl >= 4 ) {
		reps_in_par	= 4
		peaks_in_par 	= 2
	}
	else if ( par_lvl >= 3 ) {
		reps_in_par	= 2
		peaks_in_par 	= 2
	}
	else if ( par_lvl >= 2 ) {
		reps_in_par	= 2
		peaks_in_par 	= 1
	}
	else {
		reps_in_par	= 1
		peaks_in_par 	= 1		
	}
}

void print_general() {

	print("\n\n== general pipeline settings\n")
	print( "Output dir.\t\t: $out_dir\n" )
	print( "No parallel jobs\t: $no_par\n" )
	print( "Level of parallelization\t: $par_lvl\n" )
	print( "Max # rep/ctl in parallel\t: $reps_in_par\n" )
	print( "Max # peakcalling in parallel\t: $peaks_in_par\n" )
	print( "Walltime (general)\t: $wt\n" )
	print( "Max. memory (general)\t: $memory\n" )
	print( "Env. for Anaconda python2\t: $conda_env\n" )
	print( "Env. for R 2.x\t\t: $conda_env_r2\n" )
	print( "Env. for Anaconda python3\t: $conda_env_py3\n" )

	if ( use_sys_default ) {
		print( "System default resource settings (unlimited in most cases) will be used for walltime and max. memory!\n")
	}
}

void init_shell_env() {

	init_mods 	:= ["/etc/profile.d/modules.sh", "/etc/profile.d/modules.bash"] // environment modules (bash) initialization file paths (pick from the first if exists)
	init_mod 	:= "" // module init. shell script found

	// get shell var MODULESHOME if exists
	moduleshome := get_shell_var("MODULESHOME").replace("\n","")
	if (moduleshome!="") init_mods.add("$moduleshome/init/bash")

	string shellcmd, module, path, conda_py2, conda_r2, conda_py3

	// find env. modules init script
	for ( string file : init_mods ) {
		if ( file.exists() ) {
			init_mod = file
			break
		}
	}

	if ( init_mod == "" ) {
		print("\n\nInfo: Environments module not found on your system (e.g. /etc/profile.d/modules.sh). Ignoring shell env. parameters like '-mod'. \n")
	}

	// read from conf./env. file
	for( string k : conf.keys() ) {		
		string val = conf{k}	

		// concat. module
		if ( (k.indexOf("mod_")>=0) || (k=="mod") ) {

			if ( init_mod != "" ) {
				trimmed := val.trim().replace("module add ","").replace( ":", " " ).replace( ";", " " ).replace( ",", " " ).trim()
				trimmed = trimmed.replace( "   ", " ").replace( "  ", " ")
				module = module + " " + trimmed
			}
		}
		else if ( k.indexOf("shcmd")>=0 ) {

			shellcmd = shellcmd + " " + val + ";"
		}
		else if ( k.indexOf("addpath")>=0 ) {

			path = path + val.trim().replace(",",":").replace(";",":").replace(" ",":").replace(":::",":").replace("::",":") + ":"
		}
	}

	// read from cmd. line arg.
	if ( mod!="" ) {
		string module_header = ". $init_mod;"

		if ( init_mod != "" ) { // if /etc/profile.d/modules.sh exists

			trimmed := mod.trim().replace("module add ","").replace( ":", " " ).replace( ";", " " ).replace(","," " ).trim()
			trimmed = trimmed.replace( "   ", " ").replace( "  ", " ")
			module = module + " " + trimmed
		}
	}
	if ( shcmd!="" ) {
		shellcmd = shellcmd + shcmd.trim() + "; "
	}

	if ( addpath!="" ) {
		path = path + addpath.trim().replace(",",":").replace(";",":").replace(" ",":").replace(":::",":").replace("::",":") + ":"
	}

	if ( module !="" ) {
		module = ". $init_mod; module add " + module + ";"
	}

	// check script directories to add to PATH
	script_file_paths := get_script_file_paths()

	for ( string _path : script_file_paths ) {
		
		if ( _path.exists() ) {
			path = path + _path + ":"
		}
	}

	if ( path !="" ) {
		path = " export PATH=$path:\${PATH}:/bin:/usr/bin:/usr/local/bin:\${HOME}/.bds;"
	}

	// add conda env
	if ( conda_env != "" ) {
		conda_py2 = "if [[ -f $(which activate) ]]; then source activate $conda_env; fi; "
	}

	if ( conda_env_r2 != "" ) {
		conda_r2  = "if [[ -f $(which activate) ]]; then source activate $conda_env_r2; fi; "
	}
	else if ( conda_env != "" ) {
		conda_r2 = "if [[ -f $(which activate) ]]; then source activate $conda_env; fi; "
	}

	if ( conda_env_py3 != "" ) {
		conda_py3 = "if [[ -f $(which activate) ]]; then source activate $conda_env_py3; fi; "
	}

	// additional initialization
	shcmd_init_ := module + path + shellcmd
	shcmd_init_ = shcmd_init_.replace( ": :", ":" ).replace( "::", ":" ).replace( "; ;", ";" ).replace( ";;", ";" )

	shcmd_init 	= conda_py2 + shcmd_init_
	shcmd_init_r2 	= conda_r2  + shcmd_init_
	shcmd_init_py3 	= conda_py3 + shcmd_init_

	print( "\nShell command for initialization\t: $shcmd_init\n" )
	print( "\nShell command for initialization (R2)\t: $shcmd_init_r2\n" )
	print( "\nShell command for initialization (py3)\t: $shcmd_init_py3\n" )
}

string get_rel_path( string path ) {

	rel_path := path.path().replace( out_dir.path(), "." )

	if ( rel_path == path.path() ) 	return ""
	else 				return rel_path
}

int get_res_wt( string str ) {

	return (use_sys_default || is_system_local() ) ? -1 : parse_time( str )
}

int get_res_mem( string str ) {

	return (use_sys_default || is_system_local() ) ? -1 : parse_mem( str )
}

void wait_par() {

	if ( par_lvl <= 0 ) wait
}

void wait_par_rep() {

	if ( tids_rep.size() >= reps_in_par ) {
		dummy := tids_rep // fails without dummy array, it's BDS bug
		wait dummy

		if ( tids_rep.size() > 0 ) tids_rep = []
	}
}

void wait_par_peak() {

	if ( tids_callpeak.size() >= peaks_in_par ) {
		dummy := tids_callpeak // fails without dummy array, it's BDS bug
		wait dummy

		if ( tids_callpeak.size() > 0 ) tids_callpeak = []
	}
}