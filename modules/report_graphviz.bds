#!/usr/bin/env bds
#vim: syntax=java

include "report.bds"


grp_shape_box 		:= "box"
grp_shape_node 		:= "ellipse"
grp_fontsize_box 	:= 12
grp_fontsize_node 	:= 10
grp_color_node 		:= "whitesmoke"

grp_color_trim_adapter 	:= "darkorange"
grp_color_bwa 		:= "salmon"
grp_color_bwt2		:= "salmon"
grp_color_dedup_bam 	:= "lightcoral"
grp_color_xcor 		:= "yellowgreen"

grp_color_sigtrk 	:= "yellow"

grp_color_spp		:= "skyblue"
grp_color_macs2		:= "lightgreen"

grp_color_idr 		:= "limegreen" 

grp_color_ataqc 	:= "pink"


dot_lines 	:= "" // graphviz dot contents



string _html_graphviz() { // graphviz diagram

	dot_tmp := "$out_dir/workflow_tmp.dot"
	dot 	:= "$out_dir/workflow.dot"
	svg 	:= "$out_dir/workflow.svg"

	dot_tmp.write( "digraph {\n\nrankdir=LR\n\nnode [shape=$grp_shape_node,fixedsize=shape,margin=0,fontsize=$grp_fontsize_node];\nedge [fontsize=$grp_fontsize_node];\n\n" \
		   	+ dot_lines + "\n\n}" )

	// run graphviz
	task {
		sys $shcmd_init
		sys cat $dot_tmp | awk '!x[$0]++' > $dot 	# remove duplicate lines
		sys dot -Tsvg $dot > $svg 			# dot to svg
		sys rm -f $dot_tmp
	}
	// add workflow diagram
	return "<div id='graphviz'><object data='"+ get_rel_path( svg ) + "'></object></div><br>"
}

// add graphviz element (in -> box -> out) to 'dot_lines'
void _add_to_graphviz( string[] in_id, string[] in_path, string[] out_id, string[] out_path, string box, string box_color ) {

	lines := ""
	// {in1, in2, ...} -> box -> {out1, out2, ...}		

	in  := "{"; out := "{"

	found_in := false

	for ( int i=0; i<in_id.size(); i++ )  {

		//if ( in_id[i] == "" || !path_exists( in_path[i] ) ) continue
		if ( in_id[i] == "" ) continue

		found_in = true

		in += "\""+in_id[i]+"\" "

		path 	:= get_rel_path( in_path[i] )

		lines += "\t\""+in_id[i]+"\" [id=\""+in_id[i]+"\",style=filled,fillcolor=$grp_color_node,tooltip=\"$path\",href=\"$path\"];\n"
	}

	found_out := false

	for ( int i=0; i<out_id.size(); i++ ) {

		//if ( out_id[i] == "" || !path_exists( out_path[i] ) ) continue
		if ( out_id[i] == "" ) continue

		found_out = true

		out += "\""+out_id[i]+"\" "

		path 	:= get_rel_path( out_path[i] )

		lines += "\t\""+out_id[i]+"\" [id=\""+out_id[i]+"\",style=filled,fillcolor=$grp_color_node,tooltip=\"$path\",href=\"$path\"];\n"			
	}

	in  += "}"; out += "}"

	if ( box == "" ) {
		lines += "\t$in -> $out;\n"
	}
	else {
		lines += "\t\"$box\" [fontsize=$grp_fontsize_box,shape=$grp_shape_box,style=filled,fillcolor=$box_color];\n" + \
			 "\t$in -> \"$box\";\n" + \
			 "\t\"$box\" -> $out;\n"
	}

	if ( found_in || found_out ) dot_lines += lines
}

void _add_to_graphviz( string[] in_id, string[] in_path, string[] out_id, string[] out_path ) {

	_add_to_graphviz( in_id, in_path, out_id, out_path, "", "" )
}
