#!/usr/bin/env bds
#vim: syntax=java

include "general.bds"


rpt_aux_dir 	:= ""


grp_shape_box 		:= "box"
grp_shape_node 		:= "ellipse"
grp_fontsize_box 	:= 12
grp_fontsize_node 	:= 10
grp_color_node 		:= "whitesmoke"

grp_color_trim_adapter 	:= "darkorange"
grp_color_bwa 		:= "salmon"
grp_color_bwt2		:= "salmon"
grp_color_dedup_bam 	:= "lightcoral"
grp_color_xcor 		:= "yellowgreen"
grp_color_sigtrk 	:= "yellow"
grp_color_spp		:= "skyblue"
grp_color_macs2		:= "lightgreen"
grp_color_idr 		:= "limegreen" 
grp_color_ataqc 	:= "pink"


dot_lines 	:= "" // graphviz dot contents
//string[] dot_lines


init_report_graphviz()


void init_report_graphviz() {

	rpt_aux_dir 	= mkdir_path( "$out_dir/report" )
}

string _html_graphviz() { // graphviz diagram

	html := "<div id='graphviz'><b>Workflow diagram</b><br>"

	dot_tmp := "$rpt_aux_dir/workflow_tmp.dot"
	dot 	:= "$rpt_aux_dir/workflow.dot"
	svg 	:= "$rpt_aux_dir/workflow.svg"

	dot_tmp.write( "digraph {\n\nrankdir=LR\n\nnode [shape=$grp_shape_node,fixedsize=shape,margin=0,fontsize=$grp_fontsize_node];\nedge [fontsize=$grp_fontsize_node];\n\n" \
		   	+ dot_lines + "\n\n}" )

	// run graphviz
	task {
		sys $shcmd_init
		sys cat $dot_tmp | awk '!x[$0]++' > $dot 	# remove duplicate lines
		sys dot -Tsvg $dot > $svg 			# dot to svg
		sys rm -f $dot_tmp
	}

	html += "<object data='"+ get_rel_path( svg ) + "'></object></div><br>"
	html += "</div><br>\n"

	return html
}

// add graphviz element (in -> box -> out) to 'dot_lines'
void _add_to_graphviz( string[] in_id, string[] in_path, string[] out_id, string[] out_path, string box, string box_color ) {

	lines := ""
	// {in1, in2, ...} -> box -> {out1, out2, ...}		

	in  := "{"; out := "{"

	found_in := false

	for ( int i=0; i<in_id.size(); i++ )  {

		//if ( in_id[i] == "" || !path_exists( in_path[i] ) ) continue
		if ( in_id[i] == "" ) continue

		found_in = true

		in_ := in_id[i]
		in_ = in_.replace("_","\\n")

		in += "\""+ in_ +"\" "

		path 	:= "../" + get_rel_path( in_path[i] ) // ../ is because .svg is located at $out_dir/report/

		lines += "\t\""+in_+"\" [id=\""+in_+"\",style=filled,fillcolor=$grp_color_node,tooltip=\"$path\",href=\"$path\"];\n"
	}

	found_out := false

	for ( int i=0; i<out_id.size(); i++ ) {

		//if ( out_id[i] == "" || !path_exists( out_path[i] ) ) continue
		if ( out_id[i] == "" ) continue

		found_out = true

		out_ := out_id[i]
		out_ = out_.replace("_","\\n") //.substr( out_.lastIndexOf("/")+1 ).replace("_","\\n")

		out += "\""+out_+"\" "

		path 	:= "../" + get_rel_path( out_path[i] )

		lines += "\t\""+out_+"\" [id=\""+out_+"\",style=filled,fillcolor=$grp_color_node,tooltip=\"$path\",href=\"$path\"];\n"			
	}

	in  += "}"; out += "}"

	if ( box == "" ) {
		lines += "\t$in -> $out;\n"
	}
	else {
		box = box.replace("_","\\n")

		lines += "\t\"$box\" [fontsize=$grp_fontsize_box,shape=$grp_shape_box,style=filled,fillcolor=$box_color];\n" + \
			 "\t$in -> \"$box\";\n" + \
			 "\t\"$box\" -> $out;\n"
	}

	if ( found_in || found_out ) dot_lines += lines
}

void _add_to_graphviz( string[] in_id, string[] in_path, string[] out_id, string[] out_path ) {

	_add_to_graphviz( in_id, in_path, out_id, out_path, "", "" )
}
